; NeoHabitat Windows Installer script v1.0
; Script generated by Stuart Cass (https://github.com/stublad) - 03/01/2020
; Updated from VICE 3.1 to VICE 3.7 - Stuart Cass - 08/09/2023

#define MyAppName "NeoHabitat"
#define MyAppVersion "1.1"
#define MyAppPublisher "NeoHabitat"
#define MyAppURL "http://www.neohabitat.org/"
#define MyAppExeName "x64sc.exe"

[Setup]
; NOTE: The value of AppId uniquely identifies this application. Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{E031582D-6852-4F4B-9913-6B41E8F8B7F3}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={autopf}\{#MyAppName}
DisableProgramGroupPage=yes
; Uncomment the following line to run in non administrative install mode (install for current user only.)
;PrivilegesRequired=lowest
Compression=lzma
SolidCompression=yes
WizardStyle=modern

; The text file displayed after the license, with information regarding the NeoHabitat connection procedure.
InfoBeforeFile=intro.txt

; The GNU General Public License v2.0 for VICE. I'm not sure if we can also include the MIT license for Habitat too.
LicenseFile=COPYING.txt

; The compiled executable ends up here.
OutputDir=Compiled

; Self explanatory.
OutputBaseFilename=NeoHabitatInstaller

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Files]
Source: "Neohabitat\habitat.vfl"; DestDir: "{app}"; AfterInstall: WriteToFile; Flags: ignoreversion
Source: "Neohabitat\COPYING"; DestDir: "{app}"; Flags: ignoreversion;
Source: "Neohabitat\Habitat-B.d64"; DestDir: "{app}"; Flags: ignoreversion
Source: "Neohabitat\Habitat-Boot.d64"; DestDir: "{app}"; Flags: ignoreversion
Source: "Neohabitat\Habitat-Boot-Original.d64"; DestDir: "{app}"; Flags: ignoreversion
Source: "Neohabitat\NeoHabitatControls.pdf"; DestDir: "{app}"; Flags: ignoreversion
Source: "Neohabitat\NEWS"; DestDir: "{app}"; Flags: ignoreversion
Source: "Neohabitat\README"; DestDir: "{app}"; Flags: ignoreversion
Source: "Neohabitat\vice.ini"; DestDir: "{app}"; AfterInstall: WriteToIni; Flags: ignoreversion
Source: "Neohabitat\bin\*"; DestDir: "{app}\bin"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "Neohabitat\C64\*"; DestDir: "{app}\C64"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "Neohabitat\C64DTV\*"; DestDir: "{app}\C64DTV"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "Neohabitat\C128\*"; DestDir: "{app}\C128"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "Neohabitat\CBM-II\*"; DestDir: "{app}\CBM-II"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "Neohabitat\common\*"; DestDir: "{app}\common"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "Neohabitat\doc\*"; DestDir: "{app}\doc"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "Neohabitat\DRIVES\*"; DestDir: "{app}\DRIVES"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "Neohabitat\lib\*"; DestDir: "{app}\lib"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "Neohabitat\PET\*"; DestDir: "{app}\PET"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "Neohabitat\PLUS4\*"; DestDir: "{app}\PLUS4"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "Neohabitat\PRINTER\*"; DestDir: "{app}\PRINTER"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "Neohabitat\SCPU64\*"; DestDir: "{app}\SCPU64"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "Neohabitat\share\*"; DestDir: "{app}\share"; Flags: ignoreversion recursesubdirs createallsubdirs
Source: "Neohabitat\VIC20\*"; DestDir: "{app}\VIC20"; Flags: ignoreversion recursesubdirs createallsubdirs
; NOTE: Don't use "Flags: ignoreversion" on any shared system files

; This section is necessary to create the fliplist and provide the full filepath to the Habitat B disk instead of hard coding it
; It also writes to the vice.ini file to provide a full filepath to the Habitat fliplist
[Code]
procedure WriteToFile;
var app : string;
begin
  app := ExpandConstant('{app}');
  SaveStringToFile(ExpandConstant('{app}\habitat.vfl'), '; Vice fliplist file' + #13#10, True);
  SaveStringToFile(ExpandConstant('{app}\habitat.vfl'), #13#10, True);
  SaveStringToFile(ExpandConstant('{app}\habitat.vfl'), 'UNIT 8' + #13#10, True);
  SaveStringToFile(ExpandConstant('{app}\habitat.vfl'), app, True);
  SaveStringToFile(ExpandConstant('{app}\habitat.vfl'), '\Habitat-B.d64', True);
end;

procedure WriteToIni;
var app : string;
begin
  app := ExpandConstant('{app}');
  SaveStringToFile(ExpandConstant('{app}\vice.ini'), 'FliplistName="', True);
  SaveStringToFile(ExpandConstant('{app}\vice.ini'), app, True);
  SaveStringToFile(ExpandConstant('{app}\vice.ini'), '\habitat.vfl"', True);

end;

[Run]
Filename: "{app}\NeoHabitatControls.pdf"; Description: "View NeoHabitat controls PDF"; Flags: postinstall shellexec nowait skipifsilent unchecked
Filename: "{app}\bin\{#MyAppExeName}"; Description: "Launch NeoHabitat"; Parameters: "-autostart ""{app}/Habitat-Boot.d64"" -flipname ""{app}/habitat.vfl"""; Flags: postinstall nowait skipifsilent unchecked

[Icons]
Name: "{autoprograms}\{#MyAppName}"; Filename: "{app}\bin\{#MyAppExeName}"; Parameters: "-autostart ""{app}/Habitat-Boot.d64"" -flipname ""{app}/habitat.vfl"""
Name: "{autodesktop}\{#MyAppName}"; Filename: "{app}\bin\{#MyAppExeName}"; Parameters: "-autostart ""{app}/Habitat-Boot.d64"" -flipname ""{app}/habitat.vfl"""
